apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    def variants = android.hasProperty('applicationVariants') \
        ? android.applicationVariants \
        : android.libraryVariants

    source = variants.release.javaCompile.source
    classpath = files(variants.release.javaCompile.classpath.files, android.bootClasspath)

    options.links("https://docs.oracle.com/javase/7/docs/api/");
    options.linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference");
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')

    configurations = ['archives']
    pkg {
        repo = 'li.vin'
        name = 'android-net'
        userOrg = 'vinli'
        websiteUrl = 'https://github.com/vinli/android-net'
        vcsUrl = 'git://github.com/vinli/android-net.git'
        licenses = ['MIT']
        labels = ['android', 'vinli']
        publish = true
        publicDownloadNumbers = false
        version {
            name = '1.0.0-beta.1'
            vcsTag = '1.0.0-beta.1'
        }
    }
}
